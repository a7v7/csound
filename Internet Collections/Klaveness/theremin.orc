


;--------------------------------------------------------------------------------
;THEREMIN ORCHESTRA

sr        =         44100
kr        =         4410
ksmps     =         10
nchnls    =         1

gisintab  ftgen 0,0,4096,10,1                ;SINWAVE
gipulltab ftgen 0,0,4096,9,0.5,1,0           ;FOR PULLING DISTORTION, SEE BELOW

          instr     1 
;------------------------------------------------------------------------ 
;THEREMIN SIMULATION INSTRUMENT, ROGER KLAVENESS NOV.-1998, 
;CONVERTED TO CECILIA AND BACK AGAIN JAN.-1999
;----------------------------------------------------------------------
;CONTROL PARAMETERS

;ORIGINALY GENERATED BY CECILIA GLOBAL INSTRUMENT
kpitch    oscil1i   0,1,p3,1       ;    51 POINTS
kvib      oscil1i   0,1,p3,3       ;    101 POINTS
kamp      oscil1i   0,1,p3,2       ;    37 POINTS
kvibfreq  oscil1i   0,1,p3,4       ;    51 POINTS
;---------------------------------------------


;RANDOM AMP VARIATION, FOR SHAKY HANDS
 
kshakeamphand randh 0.1,10 

kamp      =         ampdb(kamp+kshakeamphand) 
kvib      oscil     kvib,kvibfreq,gisintab 

;RANDOM PITCH VARIATION, FOR SHAKY HANDS 

kshakepitchhand randh 0.01,10 
kpitch    =         cpsoct(kpitch+kvib+kshakepitchhand)

;-----------------------------------------------------------------------
;SIMULATION OF REAL THEREMIN 
;TO OSCILATORS IN RADIOFREQ ARE COMBINED WITH HETERODYNING TO MAKE AN AUDIBLE DIFFERENCE WAVE 
;ONE COULD USE A HIGH SAMPLERATE TO MAKE IT MORE LIKE THE REAL THING BUT THAT REQUIRES    
;SOMETHING LIKE SR=400kHz , AND THAT IS PROBABLY NOT NECESARRY 
;THE FREQSPACE IS DIVIDED IN 3 
;0 - sr/6 : AUDIO FREQ WE WANT, DIFF-FREQ FROM MODULATION 
;sr/6 - 2*sr/6 : FIXED AND VARIABLE OSCILATOR 
;2*sr/6 - 3*sr/6 : ALIASED SUM-FREQ FROM MODULATION 
;FIXED OSCILATOR sr/3 
;VARIABLE OSCILATOR SHOULD BE IN RANGE sr/3 -> sr/3-2000 
;FREQ OVER sr/6 IS FILTERED OUT 

;--------------------------------------------- 
;OSCILATORS 
a1        oscil     1, sr/3, gisintab             ; FIXED OSCILLATOR 
a2        oscili    1, (sr/3)-kpitch, gisintab    ; VARIABLE OSCILLATOR 
 
;--------------------------------------------- 
;PULLING DISTORTON 
;LOOK AT http://www.paia.com/thereton.htm 
;THE TWO OSCILATORS ARE MUTUALY INFLUENCING THE OTHER 
;WHICH CAUSES DISTORTION OF THEIR WAVEFORM 
;THIS EFFECT BECOMES GREATER AS THE FREQ GET LOWER 
;THIS IS JUST EXPERIMENTATION FROM MY SIDE, AND DO PROBABLY 
;NOT REFLECTS THE PHYSICS IN THIS PROCESS 
kpull     =         ((2000/kpitch)-1)/80 ;more effect at low freq, no eff > 2000Hz = sinwave 
kpull     =         (kpull < 0 ? 0 : kpull) 
kpull     =         (kpull > 0.5 ? 0.5 : kpull) ; not greater than 0.5 pull-factor
ap1shape  table     abs(a1),gipulltab,1 ; The osc get more affected at 45 degrees, just looked like that in illustration.
ap2shape  table     abs(a2),gipulltab,1 ; One could experiment with different phases of gipulltab to get different waveform, or maybe modulate it also
ap1       =         a1+kpull*ap1shape*(a2*0.5+0.5) 
ap2       =         a2+kpull*ap2shape*(a1*0.5+0.5) 
     
;--------------------------------------------- 
; HETERODYNING = AMPLITUDEMODULATION 
ap1       =         (0.5*ap1)+0.5 
amix      =         (ap1*ap2)*kamp  
 
;------------------------------------------------------------------------ 
;FILTER OUT UNWANTED FREQ

;10.ORDER BUTTERWORTH-LP, sr/6
ibgain    =         0.00001 
iagain    =         -1 
aout      zfilter2  amix,1,1,10,10,ibgain*.0027,ibgain*.0273,ibgain*.1230,ibgain*.3281,ibgain*.5742,ibgain*.6891,ibgain*.5742,ibgain*.3281,ibgain*.1230,ibgain*.0273,ibgain*.0027,iagain*-7.4907,iagain*25.4989,iagain*-51.8964,iagain*69.8813,iagain*-65.0115,iagain*42.2946,iagain*-18.9909,iagain*5.6301,iagain*-0.9948,iagain*0.0795 

;PASSING FREQUENCIES ABOVE 25Hz , GET AWAY DC
aout      butterhp  aout, 25 
aout      balance   aout,amix 

;------------------------------------------------------------------------ 
;SOME TUBE-OVERDRIVE SIMULATION
anorm     =         aout / 32767
aout      =         aout - (anorm*anorm*anorm)*10922; some distortion : x-(x^3)/3

;------------------------------------------------------------------------ 
;OUTPUT
          out       aout

;------------------------------------------------------------------------ 
          endin
